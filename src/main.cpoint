import @cpoint-raylib
import @std/print.cpoint
import @std/rand.cpoint

?[define PLAYER_SPEED 150]
?[define SQUARE_SIZE 31]

enum direction {
    top
    down
    right
    left
}

struct Fruit {
    var x : int
    var y : int
    var size : int
}

struct Snake {
    var width : int
    var height : int
}

var const screen_width : int = 800;
var const screen_height : int = 500;

var offset : struct Vector2;

func drawGrid(color : struct Color) void {
    for i : float = 0, i < screen_width/SQUARE_SIZE + 1, 1 {
        var vectorStartPos : struct Vector2
        vectorStartPos.x = SQUARE_SIZE*i + offset.x/2
        vectorStartPos.y = offset.y/2
        var vectorEndPos : struct Vector2
        vectorEndPos.x = SQUARE_SIZE * i + offset.x/2
        vectorEndPos.y = screen_height - offset.y/2
        DrawLineV(vectorStartPos, vectorEndPos, color)
    }
    for i : float = 0, i < screen_height/SQUARE_SIZE + 1, 1 {
        var vectorStartPos : struct Vector2
        vectorStartPos.x = offset.x/2
        vectorStartPos.y = SQUARE_SIZE*i + offset.y/2
        var vectorEndPos : struct Vector2
        vectorEndPos.x = screen_width - offset.x/2
        vectorEndPos.y =  SQUARE_SIZE*i + offset.y/2
        DrawLineV(vectorStartPos, vectorEndPos, color)
    }
}

func main(){
    InitWindow(screen_width, screen_height, "Snake")
    var RAYWHITE = Color {245, 245, 245, 255}
    var LIGHTGRAY = Color {200, 200, 200, 255}
    var GREEN = Color {0, 228, 48, 255}
    var RED = Color {230, 41, 55, 255}
    var pos_snake_x = 10
    var pos_snake_y = 200
    var player_direction : enum direction = direction::left
    var player_width_start = 300
    var player_height_start = 70
    var snake : struct Snake
    snake.width = player_width_start
    snake.height = player_height_start
    var fruit : struct Fruit
    fruit.x = rand::random() % 800
    fruit.y = rand::random() % 500
    fruit.size = 40
    offset.x = screen_width % SQUARE_SIZE
    offset.y = screen_height % SQUARE_SIZE

    while WindowShouldClose() == false {
        var deltaTime = GetFrameTime()
        if IsKeyDown(KEY_UP){
            player_direction = direction::top
        }
        if IsKeyDown(KEY_DOWN){
            player_direction = direction::down
        }
        if IsKeyDown(KEY_RIGHT){
            player_direction = direction::right
        }
        if IsKeyDown(KEY_LEFT){
            player_direction = direction::left
        }
        match player_direction {
            direction::top => {
                pos_snake_y = pos_snake_y - PLAYER_SPEED * deltaTime
                snake.height = player_width_start
                snake.width = player_height_start
            }
            direction::down => {
                pos_snake_y = pos_snake_y + PLAYER_SPEED * deltaTime
                snake.height = player_width_start
                snake.width = player_height_start
            }
            direction::right => {
                pos_snake_x = pos_snake_x + PLAYER_SPEED * deltaTime
                snake.width = player_width_start
                snake.height = player_height_start
            }
            direction::left => {
                pos_snake_x = pos_snake_x - PLAYER_SPEED * deltaTime
                snake.width = player_width_start
                snake.height = player_height_start
            }
        }
        BeginDrawing()
            ClearBackground(RAYWHITE)
            drawGrid(LIGHTGRAY)
            DrawRectangle(fruit.x, fruit.y, fruit.size, fruit.size, RED)
            DrawRectangle(pos_snake_x, pos_snake_y, snake.width, snake.height, GREEN)
        EndDrawing()
    }
    CloseWindow()
}
